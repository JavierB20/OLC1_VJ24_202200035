<INICIO> ::= <INSTRUCCIONES>

<INSTRUCCIONES> ::= <INSTRUCCIONES> <INSTRUCCION>
                  | <INSTRUCCION>

<INSTRUCCION> ::= <INSPRINTLN>
                | <DECLARACION>
                | <ASIGNACION>
                | <DECLARACIONVEC>
                | <ASIGNACIONVEC>
                | <DECLARACIONLSTDIN>
                | <ASIGNACIONLSTDIN>
                | <SIF>
                | <SMATCH>
                | <SWHILE>
                | <SDOWHILE>
                | <SFOR>
                | <INCREMENTO>
                | <DECREMENTO>
                | <SBREAK>
                | <SCONTINUE>
                | <METODO>
                | <SSTART_WITH>
                | <LLAMADA> FINCADENA
                | <SRETURN>

<INSPRINTLN> ::= PRINTLN PAR1 <EXPRESION> PAR2 FINCADENA

<SBREAK> ::= BREAK FINCADENA

<SCONTINUE> ::= CONTINUE FINCADENA

<SIF> ::= IF PAR1 <EXPRESION> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2 ELSE <SIF>
         | IF PAR1 <EXPRESION> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2 ELSE LLAVE1 <INSTRUCCIONES> LLAVE2
         | IF PAR1 <EXPRESION> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

<SMATCH> ::= MATCH <EXPRESION> LLAVE1 <CASOS> LLAVE2
           | MATCH <EXPRESION> LLAVE1 <SDEFAULT> LLAVE2
           | MATCH <EXPRESION> LLAVE1 <CASOS> <SDEFAULT> LLAVE2

<CASOS> ::= <CASOS> <CASO>
          | <CASO>

<CASO> ::= <EXPRESION> IGUAL MAYOR LLAVE1 <INSTRUCCIONES> LLAVE2

<SDEFAULT> ::= GUIONBAJO IGUAL MAYOR LLAVE1 <INSTRUCCIONES> LLAVE2

<SWHILE> ::= WHILE PAR1 <EXPRESION> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

<SDOWHILE> ::= DO LLAVE1 <INSTRUCCIONES> LLAVE2 WHILE PAR1 <EXPRESION> PAR2 FINCADENA

<SFOR> ::= FOR PAR1 <ASIGNACION> <EXPRESION> FINCADENA <ACTUALIZA_FOR> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

<ACTUALIZA_FOR> ::= <INCREMENTO>
                  | <DECREMENTO>
                  | ID IGUAL <EXPRESION>

<INCREMENTO> ::= ID MAS MAS FINCADENA
               | ID MAS MAS

<DECREMENTO> ::= ID MENOS MENOS FINCADENA
               | ID MENOS MENOS

<DECLARACION> ::= <MUTABILIDAD> ID DOSPUNTOS <TIPOS> FINCADENA
               | <MUTABILIDAD> ID DOSPUNTOS <TIPOS> IGUAL <EXPRESION> FINCADENA

<ASIGNACION> ::= ID IGUAL <EXPRESION> FINCADENA

<DECLARACIONVEC> ::= <MUTABILIDAD> ID DOSPUNTOS <TIPOS> CORCHETE1 CORCHETE2 IGUAL CORCHETE1 <LSTVALORES> CORCHETE2 FINCADENA

<ASIGNACIONVEC> ::= ID CORCHETE1 <EXPRESION> CORCHETE2 CORCHETE1 <EXPRESION> CORCHETE2 IGUAL <EXPRESION> FINCADENA
                  | ID CORCHETE1 <EXPRESION> CORCHETE2 IGUAL <EXPRESION> FINCADENA

<LSTVALORES> ::= <LSTVALORES> COMA <EXPRESION>
               | <EXPRESION>

<DECLARACIONLSTDIN> ::= LIST MENOR <TIPOS> MAYOR ID IGUAL NEW LIST PAR1 PAR2 FINCADENA

<SAPPEND> ::= ID PUNTO APPEND PAR1 <EXPRESION> PAR2 FINCADENA

<SREMOVE> ::= ID PUNTO REMOVE PAR1 <EXPRESION> PAR2

<METODO> ::= <TIPOS> ID PAR1 <PARAMS> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2
           | <TIPOS> ID PAR1 PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

<PARAMS> ::= <PARAMS> COMA <TIPOS> ID
           | <TIPOS> ID

<SSTART_WITH> ::= START_WITH ID PAR1 <PARAMSCALL> PAR2 FINCADENA
                | START_WITH ID PAR1 PAR2 FINCADENA

<PARAMSCALL> ::= <PARAMSCALL> COMA <EXPRESION>
               | <EXPRESION>

<LLAMADA> ::= ID PAR1 <PARAMSCALL> PAR2
            | ID PAR1 PAR2

<SRETURN> ::= RETURN <EXPRESION> FINCADENA
            | RETURN FINCADENA

<EXPRESION> ::= MENOS <EXPRESION> %prec UMENOS
              | TKNOT <EXPRESION>
              | <EXPRESION> MAS <EXPRESION>
              | <EXPRESION> MENOS <EXPRESION>
              | <EXPRESION> POTENCIA <EXPRESION>
              | <EXPRESION> MULT <EXPRESION>
              | <EXPRESION> DIV <EXPRESION>
              | <EXPRESION> MOD <EXPRESION>
              | <EXPRESION> IGUALACION <EXPRESION>
              | <EXPRESION> DIFERENCIA <EXPRESION>
              | <EXPRESION> MENORIGUAL <EXPRESION>
              | <EXPRESION> MENOR <EXPRESION>
              | <EXPRESION> MAYORIGUAL <EXPRESION>
              | <EXPRESION> MAYOR <EXPRESION>
              | <EXPRESION> TKAND <EXPRESION>
              | <EXPRESION> TKXOR <EXPRESION>
              | <EXPRESION> TKOR <EXPRESION>
              | PAR1 <TIPOS> PAR2 <EXPRESION>
              | PAR1 <EXPRESION> PAR2
              | <LLAMADA>
              | <SREMOVE>
              | ENTERO
              | DECIMAL
              | CADENA
              | CARACTER
              | VERDAD
              | FALSO
              | ID CORCHETE1 <EXPRESION> CORCHETE2 CORCHETE1 <EXPRESION> CORCHETE2
              | ID CORCHETE1 <EXPRESION> CORCHETE2
              | ID

<TIPOS> ::= TKINT
          | TKDOUBLE
          | TKCHAR
          | TKSTRING
          | TKBOOL
          | VOID

<MUTABILIDAD> ::= VAR
               | CONST
